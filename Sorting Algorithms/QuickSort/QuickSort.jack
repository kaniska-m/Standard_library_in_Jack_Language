class QuickSort {
    function void quick(Array A, int n) {
        do QuickSort.sort(A, 0, n - 1);
        return;
    }

    function void sort(Array A, int low, int high) {
        var int pi;

        if (low < high) {
            let pi = QuickSort.partition(A, low, high);
            do QuickSort.sort(A, low, pi - 1);
            do QuickSort.sort(A, pi + 1, high);
        }
        return;
    }

    function int partition(Array A, int low, int high) {
        var int pivot, i, j, temp;
        var boolean condition;

        let pivot = A[high];
        let i = low - 1;
        let j = low;
        while (j < high) {
            let condition = true;
            if (A[j] > pivot) {
                let condition = false;
            }
            if (condition) {
                let i = i + 1;
                let temp = A[i];
                let A[i] = A[j];
                let A[j] = temp;
            }
            let j = j + 1;
        }
        let temp = A[i + 1];
        let A[i + 1] = A[high];
        let A[high] = temp;
        return i + 1;
    }
}
